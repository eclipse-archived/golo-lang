/*
 * Copyright (c) 2012-2020 Institut National des Sciences AppliquÃ©es de Lyon (INSA Lyon) and others
 *
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 */

// .................................................................................................................. //

plugins {
  id 'java'
  id 'osgi'
  id 'jacoco'
  id 'application'
  id 'maven-publish'
  id 'com.jfrog.bintray' version '1.8.4'
  id 'ca.coglinc.javacc' version '2.4.0'
  id 'org.asciidoctor.convert' version '1.5.9.1'
  id 'net.nemerosa.versioning' version '2.8.2'
  id 'com.github.ben-manes.versions' version '0.20.0'
  id 'com.adarshr.test-logger' version '1.6.0'
}

// .................................................................................................................. //

repositories {
  jcenter()
  mavenLocal()
}

// .................................................................................................................. //

ext {
  goloCliMain = 'org.eclipse.golo.cli.Main'
  goloSources = fileTree('src/main/golo').include('**/*.golo')
  goloDocs = file("$buildDir/docs/golodoc")
  goloClasses = file("$buildDir/golo-classes")
}

// .................................................................................................................. //

group = 'org.eclipse.golo'
version = '3.4.0-SNAPSHOT'

// .................................................................................................................. //

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
  options.encoding = "UTF-8"
  options.compilerArgs += ["-Xlint:all", "-Xlint:-serial", "-parameters"]
}

// .................................................................................................................. //

compileJjtree {
  outputDirectory = file("$rootProject.projectDir/src/main/generated")
}

compileJavacc {
  outputDirectory = file("$rootProject.projectDir/src/main/generated")
}

task cleanGenerateFiles(type: Delete) {
  delete file("$rootProject.projectDir/src/main/generated")
}
clean.dependsOn cleanGenerateFiles

sourceSets {
  main {
    java {
      srcDir compileJjtree.outputDirectory
      srcDir compileJavacc.outputDirectory
    }
  }
}

// .................................................................................................................. //

configurations.all {
  exclude module: 'junit'
}

dependencies {
  compile 'org.ow2.asm:asm:8.0'
  compile 'com.beust:jcommander:1.60'
  compile 'com.github.rjeschke:txtmark:0.13'
  compile 'com.googlecode.json-simple:json-simple:1.1.1'

  testCompile 'org.ow2.asm:asm-util:8.0'
  testCompile 'org.ow2.asm:asm-analysis:8.0'
  testCompile 'org.hamcrest:hamcrest-all:1.3'
  testCompile 'org.skyscreamer:jsonassert:1.5.0'
  testCompile 'org.testng:testng:6.11'
  testCompile 'com.google.inject:guice:4.1.0:no_aop' // for TestNG
}

// .................................................................................................................. //

jar {
  manifest {
    name 'Eclipse Golo'
    symbolicName 'org.eclipse.golo'
    vendor 'Eclipse Golo Project'
    license 'http://www.eclipse.org/legal/epl-2.0'
    description 'A dynamic language for the JVM'
  }
  from(goloClasses)
}

// .................................................................................................................. //

task goloc(type: JavaExec, dependsOn: [compileJava, processResources]) {
  main = goloCliMain
  args = ['compile', '--output', goloClasses] + goloSources
  classpath = sourceSets.main.compileClasspath + files(compileJava.outputs) + files(processResources.outputs)
  inputs.files goloSources
  outputs.dir goloClasses
  description = 'Compiles Golo source files.'
  group = 'Build'
}

if (!project.hasProperty('bootstrap')) {
  classes.dependsOn(goloc)
}

// .................................................................................................................. //

test {
  classpath = files(project.sourceSets.test.runtimeClasspath, goloClasses)
  useTestNG()
  testLogging.showStandardStreams = project.hasProperty('consoleTraceTests')
  environment 'golo.bootstrapped', project.hasProperty('bootstrap') ? 'yes' : 'no'
  systemProperty 'golo.test.trace', project.hasProperty('traceTests') ? 'yes' : 'no'
  systemProperty 'java.awt.headless', 'true'
}

jacocoTestReport.dependsOn test

jacocoTestReport {
  additionalClassDirs = files(goloClasses)
  additionalSourceDirs = files(file('src/main/golo'))
}

// .................................................................................................................. //

processResources {
  filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
    'version'              : version,
    'golo.version'         : version,
    'gradle-plugin.version': '0.6',
    'timestamp'            : versioning.info.full
  ])
}

// .................................................................................................................. //

asciidoctorj {
  version = '1.5.4'
}

asciidoctor {
  sourceDir 'doc'
  sources {
    include 'golo-guide.adoc'
  }
  backends 'html5'
}

task assembleAsciidoc(type: Copy, dependsOn: asciidoctor) {
  from('doc/highlightjs') {
    include '**/*'
  }
  into "${asciidoctor.outputDir}/html5"
}

assemble.dependsOn assembleAsciidoc

// .................................................................................................................. //

task golodoc(type: JavaExec, dependsOn: goloc) {
  main = goloCliMain
  args = ['doc', '--format', 'html', '--output', goloDocs] + goloSources
  classpath = files(sourceSets.main.runtimeClasspath, goloClasses)
  inputs.files goloSources
  outputs.dir goloDocs
  description = 'Generates documentation of the standard Golo modules.'
  group = 'Documentation'
}

task doc(dependsOn: [asciidoctor, golodoc, javadoc]) {
  description = 'Generates all the documentations.'
  group = 'Documentation'
}

// .................................................................................................................. //

task vanillaScripts(type: CreateStartScripts) {
  outputDir = file('build/vanilla-golo')
  mainClassName = goloCliMain
  applicationName = 'vanilla-golo'
  classpath = startScripts.classpath
}

mainClassName = goloCliMain
applicationDefaultJvmArgs = [
  '-Xms256m', '-Xmx1024M', '-Xss1024M', '-server', '-XX:-TieredCompilation', '-XX:+AggressiveOpts'
]

startScripts.dependsOn vanillaScripts

// .................................................................................................................. //

task goloshScripts(type: CreateStartScripts) {
  outputDir = file('build/golosh')
  mainClassName = goloCliMain + ' shebang'
  applicationName = 'golosh'
  classpath = startScripts.classpath
}

startScripts.dependsOn goloshScripts


task golodebugScripts(type: CreateStartScripts) {
  outputDir = file('build/golo-debug')
  mainClassName = goloCliMain
  applicationName = 'golo-debug'
  defaultJvmOpts = [
    "-agentlib:jdwp=transport=dt_socket,server=y,address=6666,suspend=y",
    "-server",
    "-Dgolo.debug=true",
    "-Xdiag"
  ]
  classpath = startScripts.classpath
}
startScripts.dependsOn golodebugScripts

// .................................................................................................................. //

distributions {
  main {
    baseName = 'golo'
    contents {
      from(projectDir) {
        include 'README*'
        include 'CONTRIB*'
        include 'THIRD-PARTY'
        include 'notice.html'
      }
      into('samples') {
        from('samples') {
          include '**/*.golo'
        }
      }
      into('share') {
        from 'share'
      }
      from(golodoc) {
        into 'docs/golodoc'
      }
      from(javadoc) {
        into 'docs/javadoc'
      }
      from(asciidoctor) {
        into 'docs'
        exclude 'html5'
      }
      from(vanillaScripts) {
        into 'bin'
      }
      from(goloshScripts) {
        into 'bin'
      }
      from(golodebugScripts) {
        into 'bin'
      }
    }
  }
}

// .................................................................................................................. //

ext.mavenRepos = []

if (project.hasProperty('eclipseRepoUsername')) {
  mavenRepos.add([
    name    : 'eclipse',
    url     : 'https://repo.eclipse.org/content/repositories/golo-' + (version.endsWith('-SNAPSHOT') ? 'snapshots/' : 'releases/'),
    user    : eclipseRepoUsername,
    password: eclipseRepoPassword
  ])
}

if (project.hasProperty('bintrayRepoUsername')) {
  mavenRepos.add([
    name    : 'bintray',
    url     : 'https://api.bintray.com/maven/golo-lang/golo-lang/golo',
    user    : bintrayRepoUsername,
    password: bintrayRepoPassword
  ])
}

publishing {
  publications {
    golo(MavenPublication) {

      from components.java

      artifact sourceJar {
        classifier 'sources'
      }
      artifact javadocJar {
        classifier 'javadoc'
      }

      pom {
        name = 'Eclipse Golo Programming Language'
        description = 'Eclipse Golo: a lightweight dynamic language for the JVM.'
        url = 'http://golo-lang.org/'
        developers {
          developer {
            name = 'Golo committers'
            email = 'golo-dev@eclipse.org'
          }
        }
        licenses {
          license {
            name = 'Eclipse Public License - v 2.0'
            url = 'https://www.eclipse.org/org/documents/epl-2.0/EPL-2.0.html'
            distribution = 'repo'
          }
        }
        scm {
          url = 'https://github.com/eclipse/golo-lang'
          connection = 'scm:git:git@github.com:eclipse/golo-lang.git'
          developerConnection = 'scm:git:ssh:git@github.com:eclipse/golo-lang.git'
        }
      }
    }

  }

  repositories {
    mavenRepos.each { repo ->
      maven {
        name repo.name
        url repo.url
        credentials {
          username repo.user
          password repo.password
        }
      }
    }
  }
}

// .................................................................................................................. //

bintray {
  user = project.hasProperty('bintrayRepoUsername') ? bintrayRepoUsername : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayRepoPassword') ? bintrayRepoPassword : System.getenv('BINTRAY_KEY')
  filesSpec {
    from distZip.archivePath
    into '.'
  }
  publish = true
  pkg {
    name = 'distributions'
    repo = 'downloads'
    userOrg = 'golo-lang'
    websiteUrl = 'http://golo-lang.org/'
    licenses = ['EPL-1.0']
    labels = ['golo', 'java', 'jvm', 'language', 'eclipse']
    vcsUrl = 'https://github.com/eclipse/golo-lang.git'
    issueTrackerUrl = 'https://github.com/eclipse/golo-lang/issues'
    publicDownloadNumbers = false
    version {
      name = project.version
      vcsTag = tagFor(project.version)
    }
  }
}

def tagFor(version) {
  if (version.contains('-M')) {
    return "milestone/${version}"
  } else {
    return "release/${version}"
  }
}

bintrayUpload {
  doFirst {
    if (version.endsWith('-SNAPSHOT')) {
      throw new GradleException('Snapshot versions must not be uploaded to Bintray')
    }
  }
}

// .................................................................................................................. //

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}

javadoc {
  options.linkSource true
  options.use true
  options.noHelp true
  options.header '<link rel="icon" href="data:image/x-icon;base64,AAABAAEAFBQAAAEAIAC4BgAAFgAAACgAAAAUAAAAKAAAAAEAIAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAmd/wAAAAAA/34ADf+LAET/gwBb/4MAWv+LAD7/kQAFAAAAAAOW9AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAaX9AAAAAAA/4kAOv+GAMn/dAD/1l8A/6xRAP+pTwD/y1sA//9xAP//hgC0/4sAJgAAAAAAjvEAAAAAAAAAAAAAAAAAAAAAAAAAAAAImfQAAAAAAP+MAJ72aQD/a0EA/3REAP92RAD/cUIA/3FCAP92RAD/dEQA/25BAP//cwD//5AAeQAAAAAHlvEAAAAAAAAAAAAAAAAADJnzAAAAAAD/iwDBmVIA/3lHAP90RgD/k08A/81fAP//bwD/+24A/8VhAP+LSgD/d0cA/3RGAP/JXAD//44AmgAAAAAAgP8AAAAAAAAAAAAAAAAA/4gAmZxWAP9+SQD/gUoA//93AP//kQDw/44ArP+LAGb/jABt/44Auv+TAPjeaQD/fEoA/3xKAP/YZgD//5MAagAAAAAAAAAAHpT3AP+TAC/zbgD/gUwA/39OAP//hgD//5EAqgAAAAAAAAAAAAAAAACA/wAAAAAAAAAAAP+TAM//dAD/fkwA/3lOAP//fgD//4MACA+d9AAAAAAA/5kAuYFPAP+BTwD//3sA//+TAKQAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAJofYAAAAAAP+YANPBYwD/hFIA/65eAP//mQCMAAAAAAAAAAD/fgD/hFIA/5RZAP//kQD/AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAASnfQA/5AAHP+LAP9/VAD/fFEA//+QAOsAAAAA/44ANup2AP+LWQH/2GwA//+TAJwAAAAAAAAAAAAAAAAlSnoAAAAAAIxbAgIAAAAAAAAAAAAAAAAAAAAA/5QAzJxcAP+IVgH//4QA//+DAAj/mABV0G8A/5FeBP/4dwD//5YAZRea7wAAAAAAAAAAAAAAAACUXASFlF8E/5BZAiMAAAAAAAAAAAAAAAD/lgCXxmsA/5FeBP/+ewD//5sAJP+TAFfQcQL/lGEH//qBAP//mQBhF6LoAAAAAAAAAAAAAAAAAJhjB6qUYQf/mGMH/5hhBB8mTn8AAAAAAP+YAJPQcQL/lGEH//p7AP//kwAm/5QAQO18Av+ZZAn/1nYF//+ZAJAAAAAAAAAAAAAAAAAySG4AAAAAAJtmCtOZZAn/mWQJ/5RhBx0AAAAA/5wAwLVpB/+WYwn//4YB//+QAA//kwAL/4sE/5ZkDP+ZZgz//5wB8wAAAAAAAAAAAAAAAAAAAAAnT4EAAAAAAJ5pDdacZwz/nGcM/85zByT/mAH/kGMM/5NkDP//lAT2AAAAAAAAAAD/oQfUnmsR/55uEf//hgn//6AJfQAAAAAAAAAAAAAAAAAAAAApUoMAAAAAAKNsD9qhbBH+oWwR/t5+Df+hbBH/rG8P//+ZBZ8AAAAAAAAAAP+jClb+hA3/pG8U/6FxFP//mwz//6EMcgAAAAAqrPcAAAAAAAAAAAAtiMUAAAAAALN0Eu+kcxT/pHMU/6FxFP//kA3//5wFHSSq9AAAAAAAAAAAAP+kDce9dxX/qHMX/6hzF///kRL//6gP3P+kEW3/ng0k/6APKf+jD4P/ow/p/6ER/7l2Ff+ocxf/yXsV//+oD5IAAAAAAAAAAAAAAAAprfcA/6MXBf+hEu21fBr+rnca/6h3Gv/Gfhn//5EU//+mFP//pBX//6MU//+jFP//oxT//6MU//+OFP//oxTHAAAAACuu9wAAAAAAAAAAAAAAAAAutf8A/6YZCv+mGdfoiRr/pnsf/7F7Hf+jeR///6gX//+kFf//pBX//6QV//+kFf//pBX//6gXtgAAAAAtrvgAAAAAAAAAAAAAAAAAAAAAAAAAAAAusvkAAAAAAP+cGWf/nh33/5Mf//+UHf//qRr//6ka//+pGv//ox3//6ka8P+jGVYAAAAALrH3AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAD/phw0/6gfav+rHYX/qx2H/6wfbP+sHzQAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA/gfwAPgB8ADwAPAA4ABwAMAAMACB+BAAg/wQAIf8EAAH3gAAB44AAAeGAAAHwgAAB+AQAIPwEACB+BAAwAAwAMAAcADgAPAA+AHwAP4H8AA="/>'
  options.bottom '<link rel="stylesheet" href="http://golo-lang.org/documentation/next/styles/github.min.css"/><script src="http://golo-lang.org/documentation/next/highlight.min.js"></script><script>hljs.initHighlightingOnLoad()</script>'
  options.encoding 'utf-8'
  options.docEncoding 'utf-8'
  options.stylesheetFile file("$rootProject.projectDir/doc/stylesheet.css")
  options.addStringOption('Xdoclint:none', '-quiet')
  options.addBooleanOption("-allow-script-in-comments", true)
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  from javadoc.destinationDir
}

// .................................................................................................................. //

task buildMavenPlugin(type: Exec, dependsOn: 'publishGoloPublicationToMavenLocal') {
  workingDir 'golo-maven-plugin'
  commandLine 'mvn', 'clean', 'install'
  description 'Builds the Golo Maven plugin.'
  group 'Build'
}

task deployMavenPlugin(type: Exec, dependsOn: buildMavenPlugin) {
  workingDir 'golo-maven-plugin'
  commandLine 'mvn', 'deploy', '-P', 'release'
  description 'Deploys the Golo Maven plugin.'
  group 'Publishing'
}

task deployMavenPluginToBintray(type: Exec, dependsOn: buildMavenPlugin) {
  workingDir 'golo-maven-plugin'
  commandLine 'mvn', 'deploy', '-P', 'release', '-P', 'bintray'
  description 'Deploys the Golo Maven plugin to Bintray.'
  group 'Publishing'
}

// .................................................................................................................. //

task wrapper(type: Wrapper) {
  gradleVersion = '4.10.2'
  description 'Generates the Gradle wrapper scripts.'
}

// .................................................................................................................. //
