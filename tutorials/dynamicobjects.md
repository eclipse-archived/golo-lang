# Lovely DynamicObjects ğŸ˜

_Dynamic objects can have values and methods being added and removed dynamically at runtime._

You can think of this like the `{}` (`object`) of JavaScript (btw, I'm crazy of JavaScript) or the `DynamicObject` class of `.Net`.

## Define a DynamicObject

> **01-dynamicobjects-fields.golo**
```golo
module hello.dynamic.objects

function main = |args| {

  let jane = DynamicObject(): firstName("Jane"): lastName("Doe") # 1ï¸âƒ£
  println(jane) # 2ï¸âƒ£

  let john = DynamicObject() # 3ï¸âƒ£
  println(john) # 4ï¸âƒ£
  john: firstName("John") # 5ï¸âƒ£
  john: lastName("Doe") # 5ï¸âƒ£
  println(john) # 6ï¸âƒ£

  println("Hello I'm " + jane: firstName() + " " + jane: lastName()) # 7ï¸âƒ£

}
```

> - 1ï¸âƒ£ you can define a DynamicObject everywhere and its fields at the same time
> - 2ï¸âƒ£ it will print `DynamicObject{firstName=Jane, lastName=Doe}`
> - 3ï¸âƒ£ you can define an "empty" DynamicObject and add the fields later
> - 4ï¸âƒ£ it will print `DynamicObject{}`
> - 5ï¸âƒ£ add setters and values dynamically
> - 6ï¸âƒ£ it will print `DynamicObject{firstName=John, lastName=Doe}`
> - 7ï¸âƒ£ use of getters; and it will print `Hello I'm Jane Doe`

âœ‹ **To run it**, type this command: `golo golo --files 01-dynamicobjects-fields.golo`

## Add methods to a DynamicObject

Adding a method to a DynamicObject is like to add a closure to a field.

> **02-dynamicobjects-methods.golo**
```golo
module hello.dynamic.objects

function main = |args| {

  let jane = DynamicObject()
    : firstName("Jane")
    : lastName("Doe")
    : hello(|this| { # 1ï¸âƒ£
        println("Hello I'm " + this: firstName() + " " + this: lastName())
    })
    : message(|this, message| { # 2ï¸âƒ£
        println(
          "I'm " +
          this: firstName() + " " +
          this: lastName() + ": " +
          message
        )
    })

  jane: hello() # 3ï¸âƒ£
  jane: message("ğŸ‘‹ hello world ğŸŒ") # 4ï¸âƒ£

  jane: about(|this| -> "I'm " + this: firstName()) # 5ï¸âƒ£
  println(jane: about()) # 6ï¸âƒ£
}

```

> - 1ï¸âƒ£ the name pf the method is the name of the field; and the value is a closure with a self reference to the object as the first parameter
> - 2ï¸âƒ£ a DynamicObject method can have several parameters (with always the first parameter as a reference to the current object)
> - 3ï¸âƒ£ it will print `Hello I'm Jane Doe`
> - 4ï¸âƒ£ it will print `I'm Jane Doe: ğŸ‘‹ hello world ğŸŒ`
> - 5ï¸âƒ£ you can add a method dynamically when/where you want
> - 6ï¸âƒ£ it will print `I'm Jane`

âœ‹ **To run it**, type this command: `golo golo --files 02-dynamicobjects-methods.golo`

## Use a function as a constructor

If you need a constructor for you DynamicObject, it's easy! Use a function returning a DynamicObject:

> **03-dynamicobjects-constructors.golo**
```golo
module hello.dynamic.objects

function Human = |firstName, lastName| { # 1ï¸âƒ£
  return DynamicObject()
    : firstName(firstName)
    : lastName(lastName)
    : hello(|this| {
        println("Hello I'm " + this: firstName() + " " + this: lastName())
    })
    : message(|this, message| {
        println(
          "I'm " +
          this: firstName() + " " +
          this: lastName() + ": " +
          message
        )
    })
}

function main = |args| {

  let jane = Human("Jane", "Doe") # 2ï¸âƒ£

  jane: hello() # 3ï¸âƒ£
  jane: message("ğŸ‘‹ hello world ğŸŒ") # 4ï¸âƒ£

}
```

> - 1ï¸âƒ£ define a function returning a DynamicObject
> - 2ï¸âƒ£ call the function
> - 3ï¸âƒ£ it will print `Hello I'm Jane Doe`
> - 4ï¸âƒ£ it will print `I'm Jane Doe: ğŸ‘‹ hello world ğŸŒ`

âœ‹ **To run it**, type this command: `golo golo --files 03-dynamicobjects-constructors.golo`

â„¹ï¸ more about dynamic objects: https://golo-lang.org/documentation/next/#_dynamic_objects

**This is the end of the "DynamicObjects' tutorial"**. You can retrieve the source code here: [resources/dynamicobjects](resources/dynamicobjects).
