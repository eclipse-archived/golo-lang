# Structures

You will never find classes in Golo (you can use the classes defined in Java, but we'll talk about that later).
But don't worry, Golo offers you various means to do without classes, especially Structures.

## Define a structure

_Structure are useful to store data when the set of named entries is fixed._

> **01-structures.golo**
```golo
module hello.structure

# you can define structures in external modules

struct Human = { # 1ï¸âƒ£
 firstName, # 2ï¸âƒ£
 lastName # 2ï¸âƒ£
}

function main = |args| {

  let jane = Human("Jane", "Doe") # 3ï¸âƒ£
  # or let jane = Human(): firstName("Jane"): lastName("Doe")
  println(jane) # 4ï¸âƒ£

  let john = Human() # 3ï¸âƒ£
  println(john) # 5ï¸âƒ£
  john: firstName("John") # 6ï¸âƒ£
  john: lastName("Doe") # 6ï¸âƒ£
  println(john) # 7ï¸âƒ£

  println("Hello I'm " + jane: firstName() + " " + jane: lastName()) # 8ï¸âƒ£

}
```

> - 1ï¸âƒ£ define the structure with the `struct` keyword outside the body of the function
> - 2ï¸âƒ£ fields of the structure
> - 3ï¸âƒ£ constructor  of the structure
> - 4ï¸âƒ£ it will print `struct Human{firstName=Jane, lastName=Doe}`
> - 5ï¸âƒ£ it will print `struct Human{firstName=null, lastName=null}`
> - 6ï¸âƒ£ setters
> - 7ï¸âƒ£ it will print `struct Human{firstName=John, lastName=Doe}`
> - 8ï¸âƒ£ usage of getters, it will print `Hello I'm Jane Doe`

âœ‹ **To run it**, type this command: `golo golo --files 01-structures.golo`

## Add methods to a structure

Of course, you can augment your structure by adding methods to it ğŸ‰

> **02-structures-augmentations.golo**
```golo
module hello.structure

struct Human = {
 firstName,
 lastName
}

augment Human { # 1ï¸âƒ£
  function sayHello = |this| { # 2ï¸âƒ£
    println("Hello I'm " + this: firstName() + " " + this: lastName())
  }

  function saySomething = |self, something| { # 3ï¸âƒ£
    println(self: firstName() + " " + self: lastName() + " says:" + something)
  }
}

function main = |args| {

  let jane = Human("Jane", "Doe")
  let john = Human("John", "Doe")

  jane: sayHello() # 4ï¸âƒ£
  john: saySomething("hey! What's up?") # 5ï¸âƒ£

}
```

> - 1ï¸âƒ£ add method to the structure with the `augment` block
> - 2ï¸âƒ£ `this` is a reference to the current structure ...
> - 3ï¸âƒ£ ... but you use other words instead `this`, like `self`, `that`, `me`, ...
> - 4ï¸âƒ£ it will print `Hello I'm Jane Doe`
> - 5ï¸âƒ£ it will print `John Doe says:hey! What's up?`

âœ‹ **To run it**, type this command: `golo golo --files 02-structures-augmentations.golo`

## Named augmentations

_It is possible for augmentations to have a name. A named augmentation is a set of functions that can be applied to some classes or structures._

You can think of this like the traits you have in other languages like Groovy, Rust, Scala. Then you can share augmentations between different structures (you see, no need of classes nor inheritance ğŸ˜‰).

> **03-structures-named-augmentations.golo**
```golo
module hello.structure

struct Dog = { name }
struct Cat = { name }

augmentation runnable = { # 1ï¸âƒ£
  function run = |this| -> println(this: name() + " is running")
  function walk = |this| -> println(this: name() + " is walking")
}

augmentation woofable = { # 1ï¸âƒ£
  function woof = |this| -> println(this: name() + ": woof woof")
}

augmentation meowable = { # 1ï¸âƒ£
  function meow = |this| -> println(this: name() + ": meow meow")
}

augment Dog with runnable, woofable # 2ï¸âƒ£
augment Cat with runnable, meowable # 2ï¸âƒ£

function main = |args| {

  let kitty = Cat("Kitty")
  kitty: run() # 3ï¸âƒ£
  kitty: walk() # 4ï¸âƒ£
  kitty: meow() # 5ï¸âƒ£

  let wolf = Dog("Wolf")
  wolf: run() # 6ï¸âƒ£
  wolf: walk() # 7ï¸âƒ£
  wolf: woof() # 8ï¸âƒ£
}
```

> - 1ï¸âƒ£ you can define a named augmentation with an `augmentation` block,
> - 2ï¸âƒ£ grafting of the augmentations
> - 3ï¸âƒ£ it will print `Kitty is running`
> - 4ï¸âƒ£ it will print `Kitty is walking`
> - 5ï¸âƒ£ it will print `Kitty: meow meow`
> - 6ï¸âƒ£ it will print `Wolf is running`
> - 7ï¸âƒ£ it will print `Wolf is walking`
> - 8ï¸âƒ£ it will print `Wolf: woof woof`

âœ‹ **To run it**, type this command: `golo golo --files 03-structures-named-augmentations.golo`


â„¹ï¸ more about structures: https://golo-lang.org/documentation/next/#_structs

**This is the end of the "Structures' tutorial"**. You can retrieve the source code here: [resources/structures](resources/structures).

Now, let's have a look to the ğŸ˜ lovable **[DynamicObjects](dynamicobjects.md)**
