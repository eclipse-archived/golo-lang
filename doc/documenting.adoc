== Documenting Golo code

Of course you can document your code using comments (`#`), but who reads source code?

=== Documentation blocks

Golo provides a support for documentation blocks on modules, functions, augmentations, structs and unions, as well as fields.
Blocks are delimited by `----` and contain free-form Markdown
footnote:[ http://daringfireball.net/projects/markdown/syntax[Markdown text] ]
text.

Here is a quick example:

[source,golo]
--
----
This is a *nice* module that does a bunch of useless things.

See more at [our website](http://www.typeunsafe.org).
----
module Hello

----
Adds 2 elements, which is quite surprising given the name.

* `x` is the first argument,
* `y` is the second argument.

The following snipped prints `3`:

    let result = adder(1, 2)
    println(result)

Impressive!
----
function adder = |x, y| -> x + y
--

Sections can be added in the documentation using the normal markdown syntax. The level of the titles is adapted to the generated documentation.
For instance:

[source,golo]
--
----
Encode a linked list as cons cells.

# Examples

A list is constructed from cons

    let myList = Cons(1, Cons(2, Cons(3, Empty())))

----
union List = {
  ----
  The empty list
  ----
  Empty

  ----
  A cell in the list
  ----
  Cons = {
    ----
    The head of the list
    ----
    head,

    ----
    The tail of the list
    ----
    tail
  }
}
--

=== Rendering documentation

The `golo doc` command can render documentation in `html` (the default) or `markdown` format:

[source]
----
$ golo doc --output target/documentation src/**/*.golo
----

In addition, `golo doc` can also produce ctags `tags` file, to be used by
editors such as Vim or emacs. In this mode, the special output target `-` can
be used to print the tags on standard output, which is needed by some editors
or extensions.

Please consult `golo help` for more details.

=== Alignment

It is sometimes necessary to indent documentation blocks to match the surrounding code format.
Documentation blocks erase indentation based on the indentation level of the opening block:

[source,golo]
--
----
The most useful augmentation *ever*.
----
augment java.lang.String {

  ----
  Creates a URL from a string, as in: `let url = "http://foo.bar/plop": toURL()`.
  ----
  function toURL = |this| -> java.net.URL(this)
}
--

When generating documentation from the code above, the documentation block of the `toURL` function
is unindented of 2 spaces.

