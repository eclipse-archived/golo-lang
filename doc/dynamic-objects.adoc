== Dynamic objects

Dynamic objects can have values and methods being added and removed dynamically at runtime. You can
think of it as an enhancement over using hash maps and putting closures in them.

=== Creating dynamic objects

Creating a dynamic object is as simple as calling the `DynamicObject` function:

[source,golo]
----
let foo = DynamicObject()
----

A dynamic object can also be tagged using an arbitrary value as its kind. To create a tagged dynamic object, simply use a value in the constructor:

[source,golo]
----
let bar = DynamicObject("Bar")
----

Dynamic objects have the following *reserved* methods, that is, methods that you cannot override:

- `define(name, value)` allows to define an object property, which can be either a value or a
  closure, and
- `get(name)` gives the value or closure for a property name, or `null` if there is none, and
- `undefine(name)` removes a property from the object, and
- `mixin(dynobj)` mixes in all the properties of the dynamic object `dynobj`, and
- `copy()` gives a copy of a dynamic object, and
- `freeze()` locks an object, and calling `define` will raise an `IllegalStateException`, and
- `isFrozen()` checks whether a dynamic object is frozen or not, and
- `properties()` gives the set of entries in the dynamic object, and
- `hasMethod(name)` checks if a method is defined or not in the dynamic object, and
- `invoker(name, type)` which is mostly used by the Golo runtime internals, and
- `hasKind(value)` tests if the dynamic object is tagged with this value, and
- `sameKind(other)` tests if two dynamic objects have the same kind, and
- `fallback(handler)` defines a fallback behavior for property invocation.

Moreover, a suitable `toString` method is provided, but you can override it by defining a `toString` method.

=== Defining values

Defining values also defines getter and setter methods, as illustrated by the next example:

[source,golo]
----
let person = DynamicObject():
  define("name", "MrBean"):
  define("email", "mrbean@gmail.com")

# prints "Mr Bean"
println(person: name())

# prints "Mr Beanz"
person: name("Mr Beanz")
println(person: name())
----

Calling a setter method for a non-existent property defines it, hence the previous example can be
rewritten as:

[source,golo]
----
let person = DynamicObject(): name("MrBean"): email("mrbean@gmail.com")

# prints "Mr Bean"
println(person: name())

# prints "Mr Beanz"
person: name("Mr Beanz")
println(person: name())
----

=== Defining methods

Dynamic object methods are simply defined as closures. They must take the dynamic object object as
their first argument, and we suggest that you call it `this`. You can then define as many parameters
as you want.

Here is an example where we define a `toString`-style of method:

[source,golo]
----
local function mrbean = -> DynamicObject():
  name("Mr Bean"):
  email("mrbean@gmail.com"):
  define("toString", |this| -> this: name() + " <" + this: email() + ">")

function main = |args| {

  let bean = mrbean()
  println(bean: toString())

  bean: email("mrbean@outlook.com")
  println(bean: toString())
}
----

WARNING: You cannot overload methods, that is, providing methods with the same name but different
signatures.

[WARNING]
====

It is strongly recommended that you use `define` to create and update methods.
Consider the following example:

[source,golo]
----
let obj = DynamicObject():
  plop(|this| -> "Plop!")
----

Any call such as `obj: plop()` properly calls `plop()`. Because the dynamic object is fresh and new,
the first call to `plop` creates a property since it is currently missing.

That being said, the following would fail:

[source,golo]
----
obj: plop(|this| -> "Plop it up!")
----

Indeed, when the value of a dynamic object property is a function, it is understood to be a method,
hence calling `plop` like it would be a setter method fails because there already exists a property
that is a function, and it has a different signature. It needs to be updated as in:

[source,golo]
----
obj: define('plop', |this| -> "Plop it up!")
----

**As a rule of thumb, prefer named setters for values and `define` for methods. It is acceptable
to have named definitions for methods if and only if a call happens after the object creation and
before any call to `mixin` (remember that it injects properties from other objects, including
methods).**

====

=== Querying the properties

The `properties()` method returns a set of entries, as instances of `java.util.Map.Entry`. You can
thus write code such as:

[source,golo]
----
function dump = |obj| {
  foreach prop in obj: properties() {
    println(prop: getKey() + " -> " + prop: getValue())
  }
}
----

Because dynamic object entries mix both values and function references, do not forget that the predefined
`isClosure(obj)` function can be useful to distinguish them.

=== Defining a fallback behavior

The `fallback(handler)` method let's the user define a method that is invoked whenever the initial method dispatch fails.
Here is an example of how to define a fallback.

NOTE: Calling a setter method for a non-existent property defines it, thus the fallback is not applicable for setters.

[source,golo]
----
let dynob = DynamicObject():
  fallback(|this, method, args...| {
    return "Dispatch failed for method: " + method + ", with args: " + args: asList(): join(" ")
  })

println(dynob: casperGetter())
println(dynob: casperMethod("foo", "bar"))
----

[source]
----
Dispatch failed for method: casperGetter, with args:
Dispatch failed for method: casperMethod, with args: foo bar
----

A `delegate` function is available to ease defining a fallback function that delegates on another dynamic object. For instance:

[source,golo]
----
let t = DynamicObject("deleguee")
  : name("Zaphod")
  : define("sayHello", |this| -> "Hello, I'm " + this: name())

let s = DynamicObject("withFallback")
  : fallback(DynamicObject.delegate(t))

require(s: sayHello() == "Hello, I'm Zaphod", "error")
----

=== Kind of dynamic objects

The kind of a dynamic object can be any value. The more typical values are strings and unions (enumerations). This value is used by the `toString()`  method to display a more specific representation of the object, and defaults to the string `"DynamicObject"`. By using the two predefined methods `hasKind(value)` and `sameKind(other)`, it is possible to test for the kind of a dynamic object. Note that the kind of a created object can't be changed.

[source,golo]
----
function Person = |name, age| -> DynamicObject("Person")
    : name(name)
    : age(age)
    : define("marry", |this, other| {
      require(this: sameKind(other), "Can only marry a person")
      this: spouse(other)
      other: spouse(this)
    })

function printAge = |obj| {
  if (obj: hasKind("Person")) {
    println(obj: age())
  } else {
    println("I have no age")
  }
}
----

