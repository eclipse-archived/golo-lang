== Unions

Golo allows the definition of sum algebraic data types, also known as
http://en.wikipedia.org/wiki/Tagged_union[tagged union],
as present in many functional languages: OCaml, Haskell, Rust, Scala to
name a few.
The dual algebraic data type, the product type is provided by `struct` and `tuple`.

=== Definition

Unions are defined at the module-level:
[source,golo]
----
module sample

union Option = {
 Some = { value }
 None
}

function main = |args| {
  let aString = Option.Some("Hello")
  println(aString: value())

  let noString = Option.None()
  println(noString)
}
----

=== Usage example

Some well known usages of sum types are the following.

==== Enumerations

The plain old list of predefined values.

[source,golo]
----
union Color = {
  RED
  GREEN
  BLUE
}
----

This use is similar to Java `enum`, with the same power since Golo `union` can be extended through augmentation.

==== Option type

The monadic type as found for instance in OCaml (`Option`), Haskell (`Maybe`)
and many other languages (Rust, Scala, etc.)

[source,golo]
----
union Option = {
  Some = {value}
  None
}
----

As illustrated here, and contrary to Java `enum`, each alternative value can
have different fields. A `union` alternative type is in this respect similar to immutable `struct`.

==== Recursive data structures

The usual functional representation of linked lists:

[source,golo]
----
union ConsList = {
  List = { head, tail }
  Empty
}
----

Binary trees:

[source,golo]
----
union Tree = {
  Empty
  Leaf = { value }
  Node = { left, right }
}
----

=== JVM existence

A `union` type is compiled to an abstract JVM class. Each alternative value
type is itself compiled to a final immutable JVM class extending the abstract class.
The value classes are member classes of the abstract one.

Given:
[source,golo]
----
module sample

union Option = {
  Some = { value }
  None
}
----

three classes are generated:

1. an abstract class `sample.types.Option`,
2. a concrete final immutable inner class `sample.types.Option$Some` extending the first
one,
3. a similar class `sample.types.Option$None`.

For your convenience, the abstract class provides factories static methods for each of the possible
values, and you can't instantiate values directly, since values without fields
are actually singletons.

Note that proper definitions of `toString()`, `hashCode()` and `equals()` are
provided. These definitions are similar to the ones defined for frozen `struct`.

IMPORTANT: `union` values with fields are similar to frozen `struct`, that is
are immutable, have getters for fields and are compared by values. However,
these types does not feature the same helper methods, and can't have private
members.

=== Special testing methods

Unions feature special methods to test for the exact type of a value, as well
as members values if applicable. This allows to write readable tests, more
specially using the `match` clause, to look like destructuring match in
langages like OCaml, Haskell or Scala.

For instance, given a union defining a binary tree:
[source,golo]
----
union Tree = {
  Node = {left, right}
  Leaf = {value}
  Empty
}
----

one can match a `elt` value using:
[source,golo]
----
match {
  when elt: isEmpty() then // we have an empty value
  when elt: isLeaf(0) then // we have a leaf containing 0
  when elt: isLeaf()  then // we have a leaf (whatever the value)
  when elt: isNode(Empty(), Empty()) then // we have a node with empty children
  when elt: isNode(Leaf(42), Leaf(42)) then // we have a node whose both children contain 42
  when elt: isNode() then // we have a node, whatever the values
  otherwise // default case...
}
----

More precisely, each possible union value provides parameterless methods
testing its exact type, named `is<TypeName>`. In the tree example, three
methods are defined: `isEmpty()`, `isLeaf()` and `isNode()`.
In addition to these methods, a method with parameters is defined for every
alternative with members, here `isLeaf(value)` and `isNode(left, right)`. The arguments
are compared for equality to the members of the union value.
For instance:
[source,golo]
----
Leaf(0): isLeaf(0) # true
Leaf(42): isLeaf(0) # false
----
allowing readable test and match clauses.

A special singleton value is available to make these clauses even more
readable: the `Unknown` value. This special singleton is considered equal to
any other object (except `null`), and thus can be used in the parametrized test
methods to ignore some members. For instance, to match a `Node` with only one
child, one can use:
[source,golo]
----
let _ = Unknown.get()

function dealWithTree = |elt| -> match {
  when elt: isNode(Empty(), _) or elt: isNode(_, Empty()) then ...
    // one of the children is Empty, whatever the other one
  otherwise ...
}
----

=== Augmenting unions

Since the `union` itself is a abstract class, and each possible value is a
concrete class extending it, it is possible to augment the whole `union`, as in:

[source,golo]
----
augment Option {
  function map = |this, func| -> match {
    when this: isNone() then this
    otherwise Option.Some(func(this: value()))
  }
}
----

or just a value, as in:

[source,golo]
----
augment ConsList$Empty {
  function size = |this| -> 0
  function head = |this| -> null
  function tail = |this| -> this
}

augment ConsList$List {
  function size = |this| -> 1 + this: tail(): size()
}
----

